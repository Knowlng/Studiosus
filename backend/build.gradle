plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.2'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'com.diffplug.spotless' version '6.22.0'
    id 'com.github.node-gradle.node' version '3.1.1'
}

group = 'lt.sus'
// version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = 21
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

spotless {
    java {
        googleJavaFormat()
        lineEndings 'UNIX'
    }
}

compileJava.dependsOn('spotlessApply')

task copyStatic(type: Copy) {
    from '../frontend/public/static'
    into 'src/main/resources/static'
}

task copyTemplates(type: Copy) {
    from '../frontend/public/templates'
    into 'src/main/resources/templates'
}

processResources {
    dependsOn copyStatic, copyTemplates
    filesMatching('application.properties') {
        filter {
            it.replace('file:../frontend/public/static/', 'classpath:/static/')
               .replace('file:../frontend/public/templates/', 'classpath:/templates/')
        }
    }
}

node {
    version = '20.11.1'
    npmVersion = '10.2.4'
    download = true
    nodeProjectDir = file('../frontend')
}

task npmInstallRollup(type: NpmTask) {
    execOverrides {
        it.workingDir = file('../frontend')
    }
    args = ['install', 'rollup']
}

task npmBuild(type: NpmTask) {
    execOverrides {
        it.workingDir = file('../frontend')
    }
    inputs.file("../frontend/package.json")
    inputs.file("../frontend/package-lock.json")
    args = ['run', 'build']
}

npmBuild.dependsOn npmInstallRollup
bootJar.dependsOn npmBuild, processResources